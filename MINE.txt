'ProgramMode:RUN
'P+,NO R W
'USED LBL 1239,AELMSWHC
'STR 9+
RclV-Win 1
Text 1,1," "
M<15 Or M>40=>25->M
"_#E6A3__#E5F0__#E5F1__#E5F2__#E5F3__#E5F4__#E5F5__#E5FB__#E5FC_"->Str 1
"_#E6A6_"->Str 2:"_#E6A6_"->Str 3
"_#E69E_"->Str 4:"_#E6AE_"->Str 6
"_#E5AD_"->Str 8
StrLeft(Str 1,1)->Str 7
Str 2->Str 5:For 1->A To 5:Str 5+Str 5->Str 5:Next

Lbl 1

'CREATE DATABASE
ClrText
Locate 7,4,"_@E74C__@E76F__@E761__@E764__@E769__@E76E__@E767_..."
For 1->A To 9
{0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0}->List A
Next
'ADD MINES AND #S
For 1->A To M
Do
RanInt#(2,8)->C
RanInt#(2,22)->D
LpWhile List C[D]>=9
9->List C[D]
For C-1->F To C+1
For D-1->B To D+1
List F[B]+1->List F[B]:Next:Next:Next

Lbl 9

'DRAW BOARD
ClrText
For 1->A To 7
Locate 1,A,Str 5
Next

'GAMEPLAY

'MOVING
2->A:2->B:2->X:2->Y:0->Q
.1->H:0->O:0->N
While Getkey<>0:WhileEnd
Lbl 2
If X<>A Or Y<>B:Then 
10Frac List Y[X]->E
E=0=>Locate X-1,Y-1,Str 2
E=1=>Locate X-1,Y-1,StrMid(Str 1,Int List Y[X]+1,1)
E=2=>Red Locate X-1,Y-1,Str 4
IfEnd
10Frac List B[A]->E
E=0=>Blue Locate A-1,B-1,Str 2
E=1=>Blue Locate A-1,B-1,StrMid(Str 1,Int List B[A]+1,1)
E=2=>Magenta Locate A-1,B-1,Str 4
While Getkey=77:WhileEnd
If L=0 And D<>0 And D<>78 And D<>77 And D<>31
Then For 1->C To 100
1->L:Getkey->D:If D=0 Or D=78 Or D=77 Or D=31:Then 0->L:Break:IfEnd:Next:IfEnd

Lbl 3
Getkey->D
A->X:B->Y
D=31 Or D=78=>Goto E
D=77=>Goto M
D=49=>Goto W
D=39=>Goto 1
D=29=>Goto S
D=47=>Goto C
D=38=>A>2=>A-1->A
D=27=>A<22=>A+1->A
D=28=>B>2=>B-1->B
D=37=>B<8=>B+1->B
A<>X Or B<>Y=>Goto 2
0->L:Goto 3

'ENTRY
Lbl M
1->O
Lbl E
If Frac List B[A]=.1:Then 0->O:Goto 2:IfEnd:If O=0:Then Magenta Locate A-1,B-1,Str 3:While Getkey=78 Or Getkey=31:WhileEnd
Else 0->H:Frac List B[A]=0=>.2->H:0->O:IfEnd
If Frac List B[A]<>.1
Then Int List B[A]+H->List B[A]
List B[A]=.1=>Goto A
H=.1 And List B[A]>=9=>Goto L
IfEnd
.1->H
Goto 2

'AUTO EXPAND
Lbl A
Locate A-1,B-1,Str 7
A->F:B->G:0->E
Do
1->J:1->K
F=2=>0->J:G=2=>0->K
For F-J->C To F+1-Int (F/22)
For G-K->D To G+1-Int (G/8)
If Frac List D[C]<>.1:Then 
Int List D[C]+.1->List D[C]
Locate C-1,D-1,StrMid(Str 1,List D[C]+.9,1)
If List D[C]=.1
Then Isz E
C->List 10[E]
D->List 11[E]
IfEnd:IfEnd
Next:Next
E=0=>Break
List 10[E]->F
List 11[E]->G
E-1->E
LpWhile E<>.1

ClrList10
ClrList11
.1->H
Goto 2

'CHECK WIN
Lbl W
For 2->C To 8
For 2->G To 22
List C[G]<9 And Frac List C[G]<>.1=>Goto 2
Next:Next
List B[A]<9=>Locate A-1,B-1,StrMid(Str 1,Int List B[A]+1,1)
For 2->A To 8
For 2->B To 22
If List A[B]>=9:Then Green Locate B-1,A-1,Str 8:IfEnd
Next:Next
While Getkey<>31 And Getkey<>78 And Getkey<>39:If Getkey=29:Then 1->N:Goto S:IfEnd:Getkey=47=>Goto C:WhileEnd
Goto 1

'LOSS
Lbl L
Red Locate A-1,B-1,Str 6
For 2->A To 8
For 2->B To 22
If List A[B]>=9:Then If Frac List A[B]=.2:Then Red Locate B-1,A-1,"_#E6A0_":Else Red Locate B-1,A-1,Str 6:IfEnd:IfEnd
Next:Next
While Getkey<>31 And Getkey<>78 And Getkey<>39:If Getkey=29:Then 1->N:Goto S:IfEnd:Getkey=47=>Goto C:WhileEnd
Goto 1

'SETTINGS
Lbl S
ClrText
Locate 2,2,"MINES\:"
Locate 8,2,M
N=0=>Locate 1,7,"_@E742__@E741__@E743__@E74B_":Locate 9,7,"_@E748__@E745__@E74C__@E750_"
Locate 18,7,"_@E753__@E741__@E756__@E745_"
M->I:While Getkey<>0:WhileEnd

Do
0->G:M>=10=>1->G
M<100=>Locate 9+G,2," ":Locate 8,2,M
Frac (M/5)=0=>20->G
Frac (M/10)=0=>40->G

If F=0 And D<>0
Then For 1->C To 130
1->F:Getkey->D:D=0=>0->F:D=0=>Break:Next:IfEnd
For 1->C To 1+G
Getkey->D:Next
D=0=>0->F
D=28 And M<146=>Isz M
D=37 And M>1=>Dsz M
D=29 Or D=31=>Goto 1
LpWhile (D<>79 Or N<>0) And D<>49 And D<>59
D=49 Or D=59=>Goto H
I->M
ClrText
For 2->G To 8
For 2->C To 22
10Frac List G[C]->E
E=0=>Locate C-1,G-1,Str 2
E=1=>Locate C-1,G-1,StrMid(Str 1,Int List G[C]+1,1)
E=2=>Red Locate C-1,G-1,Str 4
Next:Next
Goto 2

'HELP
Lbl H
ClrText
Locate 2,1,"_@E753__@E748__@E749__@E746__@E754_\: _@E752__@E745__@E756__@E745__@E741__@E74C_ _@E754__@E749__@E74C__@E745_"
Locate 2,2,"_@E741__@E74C__@E750__@E748__@E741_\: _@E74D__@E741__@E752__@E74B_ _@E741__@E753_ _@E74D__@E749__@E74E__@E745_
Locate 2,3,"_@E746__#E5D4_\: _@E743__@E748__@E745__@E743__@E74B_ _@E746__@E74F__@E752_ _@E757__@E749__@E74E_"
Locate 2,4,"_@E746__#E5D5_\: _@E752__@E745__@E753__@E745__@E754_"
Locate 2,5,"_@E746__#E5D6_\: _@E753__@E745__@E754__@E754__@E749__@E74E__@E747__@E753_"
Locate 1,7,"_@E742__@E741__@E743__@E74B_"
While Getkey<>79:WhileEnd
Goto S

'CLEANUP
Lbl C
For 1->A To 8
ClrListA:Next
Return